# Maintainer: devvick <root@devvick.eu>

pkgbase=zerotier-systemd-manager
pkgname=${pkgbase}-git
pkgver=r34.6432472
pkgrel=2
pkgdesc="Get ZeroTier playing nice with systemd-networkd and -resolved"
arch=('any')
url="https://github.com/zerotier/zerotier-systemd-manager"
license=('BSD-3')
depends=('zerotier-one')
makedepends=('go' 'git')
provides=("${pkgbase}")
conflicts=("${pkgbase}" "${pkgbase}-bin")
source=('git+https://github.com/zerotier/zerotier-systemd-manager')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgbase}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgbase}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o zerotier-systemd-manager ./mgr.go
}

package() {
	cd "$srcdir/${pkgbase}"
	_output="${srcdir}/${pkgbase}"
	install -Dm644 "${_output}/contrib/zerotier-systemd-manager.service" "${pkgdir}/usr/lib/systemd/system/zerotier-systemd-manager.service"
	install -Dm644 "${_output}/contrib/zerotier-systemd-manager.timer" "${pkgdir}/usr/lib/systemd/system/zerotier-systemd-manager.timer"
	install -Dm755 "${_output}/zerotier-systemd-manager" "${pkgdir}/usr/bin/zerotier-systemd-manager"
}
